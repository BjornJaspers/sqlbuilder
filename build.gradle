buildscript {
    ext.kotlin_version = '1.0.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "kotlin"
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'osgi'

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

[compileJava, compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    options.compilerArgs << '-Xlint:unchecked'
}

repositories {
    jcenter()
    mavenCentral()
}

group = 'com.github.sqlbuilder'
version = '1.1.2'

configurations {
    embedded
    compile.extendsFrom(embedded)
}

dependencies {
    embedded "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    embedded "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-jdk-annotations:$kotlin_version"
    compile 'org.slf4j:slf4j-jdk14:1.7.7'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile 'com.h2database:h2:1.4.183'
    testCompile 'junit:junit:4.11'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.sqlbuilder'
            artifactId 'sqlbuilder'

            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "https://api.bintray.com/maven/laurentvanderlinden/maven/sqlbuilder"
            credentials {
                username = project.hasProperty('bintray_username') ? project.bintray_username : ''
                password = project.hasProperty('bintray_api_key') ? project.bintray_api_key : ''
            }
        }
    }
}

/*jar {
    from(configurations.embedded) {
        into('lib')
    }
    manifest {
        instruction 'Bundle-Description', 'SQLBuilder for SQL lovers'
        instruction 'Bundle-DocURL', 'https://github.com/laurentvdl/sqlbuilder'
        instruction 'Import-Package', '!jet.*,!kotlin.*,*'
        instruction 'Bundle-Classpath', ".,${configurations.embedded.files.collect({'lib/' + it.name}).join(',')}"
    }
}*/

defaultTasks 'build'
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
